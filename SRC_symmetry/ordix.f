


      SUBROUTINE ORDIX (X,M,N,INDX)
!
! MAKES AN INDEX TABLE OF INCREASING ORDER IN ARRAY X
!   OF SIZE N AND STRIDE M
! REF: W.H.PRESS ET AL. NUMERICAL RECIPIES. CAMBRIDGE UNIV.P. 1987
!
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION X(M,N),INDX(N)
      DO 1 J=1,N
         INDX(J)=J
   1  CONTINUE
      IF (N.LE.1) RETURN
      L=N/2+1
      IR=N
   2  CONTINUE
         IF (L.GT.1) THEN
            L=L-1
            INDXT=INDX(L)
            Q=X(1,INDXT)
         ELSE
            INDXT=INDX(IR)

            Q=X(1,INDXT)
            INDX(IR)=INDX(1)
            IR=IR-1
            IF (IR.EQ.1) THEN
               INDX(1)=INDXT
               RETURN
            ENDIF
         ENDIF
         I=L
         J=L+L
   3     IF (J.LE.IR) THEN
            IF (J.LT.IR) THEN
               IF (X(1,INDX(J)).LT.X(1,INDX(J+1))) J=J+1
            ENDIF
            IF (Q.LT.X(1,INDX(J))) THEN
               INDX(I)=INDX(J)
               I=J
               J=J+J
            ELSE
               J=IR+1
            ENDIF
         GO TO 3
         ENDIF
         INDX(I)=INDXT
      GO TO 2
      END
