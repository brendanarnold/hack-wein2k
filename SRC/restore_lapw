#!/bin/csh -f 
#
unalias rm

set name     = $0
set name     = $name:t 		#name of this script-file
set file     = `pwd`
set file     = $file:t		#tail of file-names
set savedir  = "."
set savefile = ""
unset force
unset silent
unset test

#---> path- and file-names
#
if ($#argv == 0) goto help
while ($#argv)
  switch ($1)
  case -[H|h]
    goto help
    breaksw
  case -[D|d]
    shift
    set savedir = $1
    shift
    breaksw
  case -[F|f]
    set force
    shift
    breaksw
  case -[S|s]
    if !($?test) set silent
    shift
    breaksw
  case -[T|t]
    set test
    unset silent
    shift
    breaksw
  default: 
    set savefile = $1
    shift
    breaksw
  endsw
end

if ($savefile == "") then
    set savefile = $file
endif

if !($?silent) then
    echo ""
    if !($?test) echo "Restore a calculation using mask $savedir/$savefile"
    if  ($?test) echo "Test restore using mask $savedir/$savefile"
    echo ""
endif

if !(-d $savedir) then
    echo ">>> Directory $savedir does not exist\!\!\! "
    echo ">>> Restore aborted"
    echo ""
    exit(1)
endif

touch $savedir/$savefile
set count = `echo $savedir/$savefile*|wc -w`
rm $savedir/$savefile

if ($count == 1) then
    echo ">>> No files found matching mask $savedir/$savefile\!\!\!"
    echo ">>> Restore aborted"
    echo ""
    exit(1)
endif


if ($?force) goto force

if !($?silent) echo -n "Checking for differences in input files: "

if !(-e $file.in0) then
    echo ">>> Files not found\!\!\!"
    echo ">>> Restore aborted"
    echo ""
    exit(1)
endif

if($savedir != ".") then
foreach i ($savedir/$savefile.in*)
    set ext = $i:e
    if ( -e $file.$ext ) then
	set count = `diff $i $file.$ext |wc -l`
	if ($count > 0) then
	    echo ""
	    echo ">>> $i and $file.$ext differ\!"
	    echo ">>> restore process aborted"
	    echo "   "
	    echo "    To force restore process use option -f"
	    echo ""
	    exit(1)
	endif
    endif
end
endif

if !($?silent) then
    echo "done"
    echo ""
endif

force:
if ($?test) then
    echo "These files would be restored:"
else
    if !($?silent) echo "Starting restore:"
endif
foreach i ($savedir/$savefile.struct $savedir/$savefile.clm* $savedir/$savefile.klist $savedir/$savefile.kgen $savedir/$savefile.in* $savedir/$savefile.vorb* $savedir/$savefile.dmat* $savedir/$savefile.scf2*)
    set ext = $i:e
    if !($?silent) echo "   $i -> $file.$ext"
    if !($?test) cp $i $file.$ext
end

if !($?silent) then
    echo ""
    if ($?test) then
       echo "These files would be restored with the given parameters"
    else 
       echo "Restore completed."
    endif
    echo ""
endif

exit(0)

exists:
cat<<EOF
$savedir/$to already exists. Quitting save

EOF
exit(1)

help:
cat << theend 

PROGRAM:	$0

PURPOSE:	restoring an scf calculation of WIEN
		to be called within the case-directory.
		Restores the struct and clmsum files
		as well as all input files (if available).

		If a directory is specified, files will be
		searched for in that directory.

		Specifying only a directory will search for
		files using the generic case name.


USAGE:		$name [FLAGS] [head_of_save_filename]

FLAGS:
-h           ->	help
-f           -> force $name to overwrite previous files
-d directory -> restore calculation from directory specified
-s           -> silent operation
-t           -> only test which files would be restored
theend

exit(1)
 
