#!/bin/csh -f 

switch ( $1 )
	case -f:
		unset confirm
		shift 
	breaksw
	case -h:
		goto syntax
		shift 
	breaksw
	case -d:
		shift; set file = $1
		shift 
	breaksw
	default:
		set confirm
	breaksw
endsw
if ( $#argv  != 0 ) goto syntax

if !($?file) then 
set file    = `pwd`
#set file    = $file:t		#tail of file-names
endif

cd $file
set a=`ls .running.*`

if($#a == 0) then
  echo no running WIEN2k job in    $file
  exit 0
endif

foreach i ( $a )
  set exe=`tail -1 $i`
  echo checking ${i}\: `tail -1 $i`

  if (-z $i ) then
    echo $i is empty, removed
    rm $i

  else

    set test=`echo $i | cut -d. -f3`
    ps -ef |grep "$test " | grep -v grep
    set test1=`ps -ef |grep "$test " | grep -v grep`

    if($#test1 == 0) then
      echo ps does not show process $test. 
      if ( $?confirm ) then
		echo -n "Do you want to remove $i ? [y/n] "
		set a=$<
		if ( $a != 'y' ) continue 
      endif 
      rm $i 
    endif

  endif

  set exepara=(lapw1para lapw1cpara lapw2para lapw2cpara lapwsopara lapwdmpara lapwdmcpara opticpara opticcpara irreppara qtlpara)

  foreach k ($exepara)

  if($exe[1] == $k) then
    unset remove
    set jj=`wc .$exe[1].*`
    set j=$jj[1]
    set jj=1
    while ( $j >= $jj )
      set test=(`head -$jj .$exe[1].* |tail -1| cut -c3-30`)
      set test=`echo $test |cut -f3 -d' ' `
      ps -ef |grep $test| grep -v grep |grep -v defunct
      set test1=`ps -ef |grep $test| grep -v grep`

      if($#test1 == 0) then
        echo ps does not show process $test of .$exe[1].* 
        set remove
      endif
    @ jj ++
    end
    
    if($?remove) then
        if ( $?confirm ) then
		echo -n "Do you want to remove .$exe[1].* ? [y/n] "
		set a=$<
		if ( $a != 'y' ) continue 
        endif 
        rm .$exe[1].* 
    endif
  endif
  end
#end of exepara loop


end

exit 0

syntax:	
echo ' check_lapw checks running WIEN2k job as defined in .running.* files within the current directory'
echo ' syntax:		check_lapw [-f] [-d full_path_directory] 	(-f  does not ask for confirmation to remove a leftover .running* file )'
exit 1	
