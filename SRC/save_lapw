#!/bin/csh -f 
#
unalias rm

set name     = $0
set name     = $name:t 		#name of this script-file
set file     = `pwd`
set file     = $file:t		#tail of file-names
set savedir  = "."
set savefile = ""
unset force
unset new
unset silent

#---> path- and file-names
#
if ($#argv == 0) goto help
while ($#argv)
  switch ($1)
  case -[H|h]
    goto help
    breaksw
  case -[D|d]
    shift
    set savedir = $1
    set new
    shift
    breaksw
  case -[F|f]
    set force
    set new
    shift
    breaksw
  case -[S|s]
    set silent
    set new
    shift
    breaksw
  case -[A|a]
    set new
    shift
    breaksw
  default: 
    set savefile = $1
    shift
    breaksw
  endsw
end

if ($?new) goto new
echo 'Fallback to compatibility mode with "old" save_lapw'
foreach i (clmsum clmup clmdn dmatup dmatdn dmatud vorbup vorbdn struct)
  if (! -z $file.$i && -e $file.$i ) 	cp $file.$i $savefile.$i		
end
  if (-e $file.scf )	mv $file.scf $savefile.scf 
  if (-e $file.broyd1) 	rm $file.broyd1
  if (-e $file.broyd2) 	rm $file.broyd2
 
echo 'broyden files deleted, clm*, dmat*, vorb*, scf and struct files saved under' $savefile 
exit (0)


new:
if !($?silent) echo "New version of save_lapw selected"

if ( $savedir != "" ) then
    if !(-d $savedir) then
	mkdir -p $savedir
    endif
endif

if !($?silent) then
    echo ""
    echo "Starting save:"
endif
foreach i ($file.struct $file.clmsum $file.clmup $file.clmdn $file.dmatup $file.dmatdn $file.dmatud $file.vorbup $file.vorbdn $file.kgen $file.klist $file.scf $file.scf2 $file.scf2up $file.scf2dn $file.in* )
    if ($savefile == "") then
	set to = $i
    else
	set to = $savefile.$i:e
    endif
    if (-e $savedir/$to)  then
	if !($?force) goto exists
    endif
    if !($?silent) echo "   $i -> $savedir/$to"
    if (! -z $i && -e $i) cp $i $savedir/$to
end

if (-e $file.scf )	rm $file.scf 
if (-e $file.broyd1) 	rm $file.broyd1
if (-e $file.broyd2) 	rm $file.broyd2
 
if !($?silent) then
    echo ""
    echo 'broyden files deleted, clm*, dmat*, vorb*, scf, struct and input files saved under' $savedir/$to:r
    echo ""
endif
exit(0)

exists:
cat<<EOF
$savedir/$to already exists. Quitting save

EOF
exit(1)

help:
cat << theend 

PROGRAM:	$0

PURPOSE:	saving (and clearing) an scf calculation of WIEN
		to be called within the case-directory
		saves the struct clmsum and scf files, deletes broyden files.

		If a save directory or save _all_ input files is specified, 
		also all input files and klist and kgen files will be
		saved. Specifying only a save directory without a
		save_file_name will cause save_lapw to save the
		abovementioned files under their generic name.

		NOTE: To use restore_lapw you have to utilize the 
		      new scheme!

USAGE:		$name [FLAGS] [head_of_save_filename]

FLAGS:
-h           ->	help
-a           -> save _all_ input files as well (new scheme)
-f           -> force save_lapw to overwrite previous saves
-d directory -> save calculation in directory specified
-s           -> silent operation
theend

exit(1)
 
